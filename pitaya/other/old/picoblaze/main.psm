ADDRESS 000
JUMP _init
JUMP _init
JUMP _init
JUMP _init
_init:

CONSTANT PORT_IO,00
CONSTANT UART_DR,01
CONSTANT UART_CR,02 

CONSTANT MEM_A_L, 03
CONSTANT MEM_A_H, 04
CONSTANT MEM_D_L, 05
CONSTANT MEM_D_H, 06
CONSTANT MEM_CR, 07


CONSTANT UART_BIT_RDA,80 	 ; rx data ready  |
CONSTANT UART_BIT_TXF,40     ; tx buf full	  |- both have 16B FIFO
CONSTANT MEM_BIT_WR, 02		 ; write to BRAM
CONSTANT MEM_BIT_RD, 01		; debug - trigger bit to ILA

 CONSTANT CHAR_CR, 0D
 CONSTANT CHAR_LF, 0A
 CONSTANT CHAR_SPC,20

;--------------------------------------------------------------
;  MAIN Loop
;--------------------------------------------------------------

REGBANK A
main:
	
	load sA, 00 ; ADDRESS Low
	load sB, 00 ; Address High

	load sC, 00 ; Data

	call uart_get_byte
loop:
		
	output	sA, PORT_IO
	output	SA, MEM_A_L
	output	sB, MEM_A_H

	load	s0, MEM_BIT_RD
	output	s0, MEM_CR
	input	sC, MEM_D_L	

	;// print Address
	load	s0,SB
	call	uart_send_hex
	load	s0,sA
	call	uart_send_hex
	load	s0, CHAR_SPC
	call	uart_send_byte

	;// print data
	load	s0, sC
	call uart_send_hex
	call uart_newline

	;// increment address
	add		sA, 01
	addcy	SB,00

	call delay_20ms

	;// check for 2048
	compare		sA,00
	comparecy	sB,01

	jump	z, main 
 
	jump loop

	jump main

;--------------------------------------------------------------
;  Subroutines
;--------------------------------------------------------------

; s0 (s1)
uart_send_byte:
	input	s1,UART_CR
	test	s1,UART_BIT_TXF
	jump	nz,uart_send_byte
	output	s0, UART_DR
	RETURN

;  retval: s0  
uart_get_byte:
	input   s0, UART_CR
	test 	s0, UART_BIT_RDA
	jump     z, uart_get_byte
	input   s0, UART_DR
	RETURN 


;  (s0,s1)
uart_newline:
	load s0,CHAR_LF
	call uart_send_byte
	load s0,CHAR_CR
	call uart_send_byte
	RETURN

; s0 (s1,s2)
uart_send_hex:
	load	s2,s0 ; backup s0
	SR0		s0
	SR0		s0
	SR0		s0
	SR0		s0
	;high part
	compare s0,0a
	jump	c,zerotonine ; 0..9
	add		s0,07		; diff to 'A' 
zerotonine:
	add		s0,30 ;'0'
	call	uart_send_byte
	;low part
	load	s0,s2
	and		s0,0f
	compare s0,0a
	jump	c,zerotonine_l ; 0..9
	add		s0,07		; diff to 'A' 
zerotonine_l:
	add		s0,30 ;'0'
	call	uart_send_byte
	RETURN 

; Registers used s0, s1, s2
include "soft_delays_100mhz.psm"
;include "i2c_routines.psm"

;--------------------------------------------------------------
;  ISR
;--------------------------------------------------------------
_isr:
REGBANK B


REGBANK A
RETURNI ENABLE

ADDRESS 3FF
jump _isr
